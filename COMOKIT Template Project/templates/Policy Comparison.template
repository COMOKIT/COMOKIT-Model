/***
* Name: $TITLE$
* $DESC$
* Based on a template model, part of COMOKIT. Its goal is to create simple experiments on a specific policy. 
* Release 1.0, May 2020. see http://comokit.org for support and updates
* Author: $AUTHOR$
* Tags: covid19,epidemiology
***/

model $TITLE$ 

import "All COMOKIT.gaml"

global {

	// Parameter file folder
	// by default parameter files are placed in Parameters/ folder of COMOKIT
	// if you decide to redefine the path to that folder identify the place COMOKIT can find your parameter files
	string parameter_folder_path <- "../../COMOKIT/Parameters/";

	string dataset_path <- "../Datasets/Case Study/"; // Relative path to the folder containing the boundary.shp, buildings.shp, satellite.png, etc. datafiles. 
	string POLICY_NAME <- "your_policy"; // Not mandatory but will be used in GUI
	
	// List of variable to compare policy with
	list list_of_values_to_compare <- [50,100,200,500]; // Example of list of number of tests per day
	bool only_symptomatic <- true; // Variable relevant only for detection policy
	bool only_not_tested <- true; //  Variable relevant only for detection policy
	
}

experiment $TITLE$ parent: "Abstract Experiment" {

	// In order to compare simulation within the same GUI, one has to launch several simulations:
	// Each simulation is created with a given parameter	
	action _init_ {
		
		// Loop over the variable of policies to compare 
		loop nb_tests over:list_of_values_to_compare {
			// Create a simulation with a given policy
			create simulation with: [dataset_path::dataset_path] {	
	 			name <- POLICY_NAME + " - Value = " + nb_tests;
	 					
				ask Authority {
					// ***************************************************************************
					// SEE Model/Entities/Authority.gaml for built-in functions to create policies
					// ***************************************************************************
					AbstractPolicy detection <- create_detection_policy(nb_tests, only_symptomatic, only_not_tested); // Create a detection policy with the given number of tests
					AbstractPolicy stay_home <- create_positive_at_home_policy(); // Infected Individual must stay home
					policy <- combination([detection, stay_home]);
				}
			}
		}	
		
	}
	
	// Setup a chart to follow the number of infectious cases over the simulations
	permanent {
		display "charts" toolbar: false background: #black refresh: every(24 #cycle) {
			chart "Infectious cases" background: #black axes: #black color: #white title_font: default legend_font: font("Helvetica", 12, #bold) title_visible: true {
				loop s over: simulations {
					data s.name value: s.number_of_infectious color: s.color marker: false style: line thickness: 2;
				}
			}
		}
	}

	// The block where the user interface of each simulation is declared
	output {
		layout #split consoles: false editors: false navigator: false tray: false tabs: false toolbars: false; // GUI components can be enabled/disabled
		display "Main" parent: default_display {
			species SpatialPolicy {
				draw application_area empty: true color: #red;
			}
		}
	}
}
